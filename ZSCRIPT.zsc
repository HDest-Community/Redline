version "4.0"

class RedlineHandler : EventHandler
{
	override void CheckReplacement(ReplaceEvent e)
	{
		if (!e.Replacement)
		{
			return;
		}

		switch (e.Replacement.GetClassName())
		{
			case 'PlasmaReplaces':
				if (random[redrand]() <= 32)
				{
					e.Replacement = "RedlineRandom";
				}
				break;
		}
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		let RedlineAmmo = HDBattery(e.Thing);
		if (RedlineAmmo)
		{
			RedlineAmmo.ItemsThatUseThis.Push("HDRedline");
		}
	}
}

class HDRedline : HDCellWeapon
{
	enum RedlineFlags
	{
		RDF_HeatSink = 1,
		RDF_Scope = 2,
		RDF_Capacitor = 4,
		RDF_Overheated = 16,
	}

	enum RedlineProperties
	{
		RDProp_Flags,
		RDProp_Battery,
		RDProp_Charges,
		RDProp_LoadType,
		RDProp_Heat
	}

	override void PostBeginPlay()
	{
		weaponspecial = 1337; // [Ace] UaS sling compatibility.

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		if (WeaponStatus[RDProp_Heat] > 0)
		{
			if (WeaponStatus[RDProp_Flags] & RDF_Overheated)
			{
				WeaponStatus[RDProp_Heat]--;
			}
			if (WeaponStatus[RDProp_Flags] & RDF_HeatSink)
			{
				WeaponStatus[RDProp_Heat]--;
			}
			DrainHeat(RDProp_Heat, 10, 0.5, 1.5, 0.6);
		}
		else
		{
			WeaponStatus[RDProp_Flags] &= ~RDF_Overheated;
			WeaponStatus[RDProp_Heat] = 0;
		}


		Super.Tick();
	}

	override void DetachFromOwner()
	{
		owner.A_StopSound(100);
		Super.DetachFromOwner();
	}

	override void OnDestroy()
	{
		if (owner)
		{
			owner.A_StopSound(100);
		}
		Super.OnDestroy();
	}

	override bool AddSpareWeapon(actor newowner) { return AddSpareWeaponRegular(newowner); }
	override HDWeapon GetSpareWeapon(actor newowner , bool reverse, bool doselect) { return GetSpareWeaponRegular(newowner, reverse, doselect); }
	override double GunMass() { return WeaponStatus[RDProp_Battery] >= 0 ? 12 : 11; }
	override double WeaponBulk()
	{
		return 122
		+ (WeaponStatus[RDProp_Battery] >= 0 ? ENC_BATTERY_LOADED : 0)
		+ (WeaponStatus[RDProp_Flags] & RDF_Scope ? 6 : 0);
	}
	override string, double GetPickupSprite() { return WeaponStatus[RDProp_Flags] & RDF_Scope ? "RDSGZ0" : "RDNGZ0", 1.0; }
	override void InitializeWepStats(bool idfa)
	{
		WeaponStatus[RDProp_Battery] = 30;
		WeaponStatus[RDProp_Charges] = WeaponStatus[RDProp_Flags] & RDF_Capacitor ? 2 : 1;
	}
	override void LoadoutConfigure(string input)
	{
		if (GetLoadoutVar(input, "sink", 1) > 0)
		{
			WeaponStatus[RDProp_Flags] |= RDF_HeatSink;
		}
		if (GetLoadoutVar(input, "cap", 1) > 0)
		{
			WeaponStatus[RDProp_Flags] |= RDF_Capacitor;
		}
		if (GetLoadoutVar(input, "scope", 1) > 0)
		{
			WeaponStatus[RDProp_Flags] |= RDF_Scope;
		}

		InitializeWepStats(false);
	}

	override string GetHelpText()
	{
		return WEPHELP_FIRE.."  Shoot\n"
		..WEPHELP_ALTFIRE.."  Work the bolt\n"
		..WEPHELP_ALTFIRE.." (hold)+"..WEPHELP_RELOAD.."  Load/Unload battery\n"
		..WEPHELP_ALTFIRE.." (hold)+"..WEPHELP_UNLOAD.."  Unload battery";
	}

	override string PickupMessage()
	{
		string capStr = WeaponStatus[RDProp_Flags] & RDF_Capacitor ? "high-capacity " : "";
		string sinkStr = WeaponStatus[RDProp_Flags] & RDF_HeatSink ? "heat-sinked " : "";
		return String.Format("You got the %s%sRDL-N3 'Redline' bolt-action thermal lance.", capStr, sinkStr);
	}

	override void DrawHUDStuff(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl)
	{
		if (sb.HudLevel == 1)
		{
			sb.DrawBattery(-54, -4, sb.DI_SCREEN_CENTER_BOTTOM, reloadorder: true);
			sb.DrawNum(hpl.CountInv("HDBattery"), -46, -8, sb.DI_SCREEN_CENTER_BOTTOM);
		}

		if (hdw.WeaponStatus[RDProp_Flags] & RDF_Overheated)
		{
			sb.DrawString(sb.pNewSmallFont, "OVERHEAT", (-24, -25), sb.DI_TEXT_ALIGN_RIGHT | sb.DI_SCREEN_CENTER_BOTTOM, Font.CR_RED, scale: (0.5, 0.5));
		}

		int yoff = -4;

		int BatteryCharge = AceCore.GetRealBatteryCharge(hdw.WeaponStatus[RDProp_Battery], 1.5, true);
		if (BatteryCharge > 0)
		{
			sb.DrawWepNum(BatteryCharge, 20, posy: yoff);
		}
		else if (BatteryCharge == 0)
		{
			sb.DrawString(sb.mAmountFont, "00000", (-16, yoff - 2), sb.DI_TEXT_ALIGN_RIGHT | sb.DI_SCREEN_CENTER_BOTTOM, Font.CR_DARKGRAY);
		}

		yoff -= 3;
		for (int i = 0; i < hdw.WeaponStatus[RDProp_Charges]; ++i)
		{
			sb.DrawRect(-16 - 7 * i, yoff - 2, -6, 2);
		}

		yoff -= 3;
		sb.DrawWepNum(hdw.WeaponStatus[RDProp_Heat], A_GetMaxHeat(), posy: yoff, alwaysprecise: true);
	}

	override void DrawSightPicture(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl, bool sightbob, vector2 bob, double fov, bool scopeview, actor hpc, string whichdot)
	{
		int cx, cy, cw, ch;
		int ScaledYOffset = 48;
		int ScaledWidth = 89;

		[cx, cy, cw, ch] = Screen.GetClipRect();
		sb.SetClipRect(-16 + bob.x, -4 + bob.y, 32, 16, sb.DI_SCREEN_CENTER);
		vector2 bob2 = bob * 2;
		bob2.y = clamp(bob2.y, -8, 8);
		sb.DrawImage("REDFRONT", bob2, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, alpha: 0.9, scale: (1, 0.6));
		sb.SetClipRect(cx, cy, cw, ch);
		sb.DrawImage("REDBACK", bob, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, scale: (1.25, 0.85));

		if (hdw.WeaponStatus[RDProp_Flags] & RDF_Scope && scopeview)
		{
			TexMan.SetCameraToTexture(hpc, "REDLNCAM", 4);
			sb.DrawImage("REDLNCAM", (0, ScaledYOffset) + bob, sb.DI_SCREEN_CENTER | sb.DI_ITEM_CENTER);
			sb.DrawImage("RDLNSCOP", (0, ScaledYOffset) + bob, sb.DI_SCREEN_CENTER | sb.DI_ITEM_CENTER);
			bob2 *= 3;
			double DotOffset = max(abs(bob2.x), abs(bob2.y));
			if (DotOffset < 20)
			{
				sb.DrawImage("RDLNSGHT", (0, ScaledYOffset) + bob2 * 3, sb.DI_SCREEN_CENTER | sb.DI_ITEM_CENTER, alpha: 0.8 - DotOffset * 0.12);
			}
		}
	}

	private action void A_CheckOverheat()
	{
		if (invoker.WeaponStatus[RDProp_Heat] > A_GetMaxHeat())
		{
			invoker.WeaponStatus[RDProp_Flags] |= RDF_Overheated;
			invoker.owner.A_StartSound("Redline/Overload", 10);
		}
	}

	private clearscope action int A_GetMaxHeat(bool raw = false)
	{
		double mult = 1.0;
		if (!raw)
		{
			if (AceCore.CheckForItem(invoker.owner, 'HDGungnir'))
			{
				mult += 0.5;
			}
		}
		return int(150 * mult);
	}

	const MaxCharges = 1;

	private bool HasFired;

	Default
	{
		-HDWEAPON.FITSINBACKPACK
		Weapon.SelectionOrder 300;
		Weapon.SlotNumber 6;
		Weapon.SlotPriority 1.5;
		HDWeapon.BarrelSize 35, 1.5, 2;
		Scale 0.6;
		Tag "RDL-N3 'Redline' Thermal Lance";
		HDWeapon.Refid "rdl";
	}

	States
	{
		Spawn:
			RDSG Z 0 NoDelay A_JumpIf(invoker.WeaponStatus[RDProp_Flags] & RDF_Scope, 2);
			RDNG Z 0;
			#### Z -1;
			Stop;
		Ready:
			RDSG A 0 A_JumpIf(invoker.WeaponStatus[RDProp_Flags] & RDF_Scope, 2);
			RDNG A 0;
			#### A 1
			{
				if (!PressingFire())
				{
					invoker.HasFired = false;
				}
				A_WeaponReady(WRF_ALL | (invoker.HasFired ? WRF_NOPRIMARY : 0));
			}
			Goto ReadyEnd;
		Select0:
			RDSG A 0 A_JumpIf(invoker.WeaponStatus[RDProp_Flags] & RDF_Scope, 2);
			RDNG A 0;
			#### A 0;
			Goto Select0Big;
		Deselect0:
			RDSG A 0 A_JumpIf(invoker.WeaponStatus[RDProp_Flags] & RDF_Scope, 2);
			RDNG A 0;
			#### A 0;
			Goto Deselect0Big;
		User3:
			#### A 0 A_MagManager("HDBattery");
			Goto Ready;
		Fire:
			#### A 1
			{
				if (invoker.WeaponStatus[RDProp_Flags] & RDF_Overheated)
				{
					A_StartSound("Redline/Overheated", CHAN_WEAPON);
					SetWeaponState('Nope');
					return;
				}

				if (invoker.WeaponStatus[RDProp_Charges] > 0 && !invoker.HasFired)
				{
					invoker.HasFired = true;
					SetWeaponState('Shoot');
					return;
				}
			}
			Goto Ready;
		Shoot:
			#### F 1 Bright Offset(0, 42)
			{
				A_Light0();
				A_StartSound("Redline/Fire", CHAN_WEAPON);

				int dmg = random(90, 120);
				A_RailAttack(dmg, 0, false, "", "", RGF_NOPIERCING | RGF_NORANDOMPUFFZ | RGF_SILENT, 0, "RedlineRayImpact", 0, 0, 12600, 0, 10.0, 0, "RedlineRaySegment");
				
				A_CheckOverheat();
				invoker.WeaponStatus[RDProp_Heat] += int((dmg / 3) * frandom(0.925, 1.05));
				invoker.WeaponStatus[RDProp_Charges]--;

				A_AlertMonsters();
				A_MuzzleClimb(0, 0, -0.1, -0.5, -frandom(0.3, 0.6), -frandom(0.3, 0.6), -frandom(0.3, 0.6), -frandom(0.3, 0.6));
			}
			#### A 2 Offset(0, 38);
			#### A 1 Offset(0, 34);
			Goto Ready;
		AltFire:
			#### A 1 Offset(0, 34) A_WeaponBusy();
			#### B 1 Offset(4, 38);
			#### B 1 Offset(0, 34);
			#### B 0 A_Refire('BoltPull');
			Goto Ready;
		BoltPull:
			#### C 2 Offset(4, 38)
			{
				A_WeaponBusy(true);
				A_StartSound("Redline/BoltBack", 8, CHANF_OVERLAP);
			}
			#### D 3 Offset(6, 42)
			{
				if (GunBraced())
				{
					A_MuzzleClimb(frandom(-0.1, 0.3), frandom(-0.1, 0.3));
				}
				else
				{
					A_MuzzleClimb(frandom(-0.2, 0.8), frandom(-0.4, 0.8));
				}
				if (invoker.WeaponStatus[RDProp_Battery] == 0)
				{
					A_StartSound("Redline/Eject", 9, pitch: 0.7);
					Actor Mag = HDMagAmmo.SpawnMag(self, "HDBattery", 0);
					Mag.Angle = Angle;
					Mag.A_ChangeVelocity(-0.75, -1.5, 3.5, CVF_RELATIVE);
					invoker.WeaponStatus[RDProp_Battery] = -1;
				}
				invoker.HasFired = false;
			}
			#### D 3 Offset(7, 44);
			#### E 3 Offset(8, 46);
			#### E 0
			{
				A_WeaponBusy(false);
				A_Refire("AltHold");
			}
			Goto AltHoldEnd;
		AltHold:
			#### E 1;
			#### E 1
			{
				A_ClearRefire();
				if ((PressingReload() || PressingUnload()) && invoker.WeaponStatus[RDProp_Battery] >= 0)
				{
					invoker.WeaponStatus[RDProp_LoadType] = 0; // [Ace] Unload.
					SetWeaponState('LoadChamber');
					return;
				}
				else if (PressingReload() && CheckInventory("HDBattery", 1))
				{
					invoker.WeaponStatus[RDProp_LoadType] = 1;
					SetWeaponState('LoadChamber');
					return;
				}

				if (PressingAltFire())
				{
					SetWeaponState('AltHold');
					return;
				}
			}
		AltHoldEnd:
			#### D 2
			{
				A_StartSound("Redline/BoltFwd", 8, CHANF_OVERLAP);
				if (invoker.WeaponStatus[RDProp_Battery] > 0 && invoker.WeaponStatus[RDProp_Charges] < MaxCharges + (invoker.WeaponStatus[RDProp_Flags] & RDF_Capacitor ? 1 : 0))
				{
					A_StartSound("Redline/Charge", 9, pitch: 0.80);
					invoker.WeaponStatus[RDProp_Charges] = invoker.WeaponStatus[RDProp_Flags] & RDF_Capacitor ? 2 : 1;
					invoker.WeaponStatus[RDProp_Battery]--;
				}
			}
			#### C 2;
			#### B 2 Offset(2, 36);
			Goto Ready;
		LoadChamber:
			#### E 1 Offset(2, 36) A_ClearRefire();
			#### E 1 Offset(3, 38);
			#### E 1 Offset(5, 42);
			#### E 1 Offset(8, 48) A_StartSound("weapons/pocket", 9);
			#### E 1 Offset(9, 52) A_MuzzleClimb(frandom(-0.2, 0.2), 0.2, frandom(-0.2, 0.2), 0.2, frandom(-0.2, 0.2), 0.2);
			#### E 2 Offset(8, 60);
			#### E 2 Offset(7, 72);
			#### E 2 Offset(6, 80);
			#### E 15;
			#### E 4
			{
				switch (invoker.WeaponStatus[RDProp_LoadType])
				{
					case 0:
						int BatteryCharge = AceCore.GetRealBatteryCharge(invoker.WeaponStatus[RDProp_Battery], 1.5, false); // [Ace] Lose fractions if you take out a non-empty battery.
						invoker.WeaponStatus[RDProp_Battery] = -1;
						if (A_JumpIfInventory("HDBattery", 0, "null"))
						{
							HDMagAmmo.SpawnMag(self, "HDBattery", BatteryCharge);
						}
						else
						{
							HDMagAmmo.GiveMag(self, "HDBattery", BatteryCharge);
						}
						A_StartSound("Redline/CellOut", 10);
						break;
					case 1:
						let Battery = HDMagAmmo(FindInventory("HDBattery"));
						if (Battery && Battery.Amount > 0)
						{
							invoker.WeaponStatus[RDProp_Battery] = int(Battery.TakeMag(true) * 1.5);
							A_StartSound("Redline/CellIn", 10);
						}
						break;
				}
			}
			#### E 2 Offset(6, 80);
			#### E 2 Offset(7, 72);
			#### E 2 Offset(8, 60);
			#### E 1 Offset(7, 52);
			#### E 1 Offset(5, 42);
			#### E 1 Offset(3, 38);
			#### E 1 Offset(3, 35);
			Goto AltHold;
	}
}

class RedlineRandom : IdleDummy
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				A_SpawnItemEx("HDBattery", -3, flags: SXF_NOCHECKPOSITION);
				let wpn = HDRedline(Spawn("HDRedline", pos, ALLOW_REPLACE));
				if (!wpn)
				{
					return;
				}

				HDF.TransferSpecials(self, wpn);
				if (!random(0, 3))
				{
					wpn.WeaponStatus[wpn.RDProp_Flags] |= wpn.RDF_HeatSink;
				}
				if (!random(0, 3))
				{
					wpn.WeaponStatus[wpn.RDProp_Flags] |= wpn.RDF_Capacitor;
				}
				if (!random(0, 3))
				{
					wpn.WeaponStatus[wpn.RDProp_Flags] |= wpn.RDF_Scope;
				}

				wpn.InitializeWepStats(false);
			}
			Stop;
	}
}

class RedlineRayImpact : HDActor
{
	Default
	{
		+NODAMAGETHRUST
		+FORCEDECAL
		+PUFFGETSOWNER
		+ALWAYSPUFF
		+PUFFONACTORS
		+NOINTERACTION
		+BLOODLESSIMPACT
		+FORCERADIUSDMG
		+NOBLOOD
		Decal "RedlineScorch";
		DamageType "Hot";
	}

	States
	{
		Spawn:
			TNT1 A 5 NoDelay
			{
				A_Explode(random(15, 30), 20, XF_HURTSOURCE, false);
				A_StartSound("Redline/Impact");

				for (int i = 0; i < 30; ++i)
				{
					double pitch = frandom(-85.0, 85.0);
					A_SpawnParticle(0xFF1111, SPF_RELATIVE | SPF_FULLBRIGHT, random(10, 20), random(6, 9), random(0, 359), random(0, 4), 0, 0, random(1, 5) * cos(pitch), 0, random(1, 5) * sin(pitch), 0, 0, -0.5);
				}
			}
			Stop;
	}
}

class RedlineRaySegment : Actor
{
	override void PostBeginPlay()
	{
		for (int i = 0; i < 2; ++i)
		{
			A_SpawnParticle(0xFF3333, SPF_RELATIVE | SPF_FULLBRIGHT, random(25, 40), frandom(0.5, 1.5), 0,random(-10, 10), 0, 0,
				frandom(-0.20, 0.20), frandom(-0.20, 0.20), frandom(-0.20, 0.20),
				frandom(-0.01, 0.05), frandom(-0.05, 0.05), frandom(-0.01, 0.01));
		}

		Super.PostBeginPlay();
	}

	Default
	{
		RenderStyle "Add";
		+NOINTERACTION
		+NOBLOCKMAP
		+BRIGHT
		Alpha 2.0;
	}

	States
	{
		Spawn:
			RDLR A 1 Bright
			{
				A_FadeOut(0.1);
			}
			Loop;
	}
}